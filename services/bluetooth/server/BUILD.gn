# Copyright (C) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/ohos_var.gni")
import("//foundation/communication/bluetooth/bluetooth.gni")

SUBSYSTEM_DIR = "//foundation/communication"
BT_ROOT = "//foundation/communication/bluetooth/services/bluetooth"

ohos_shared_library("bluetooth_server") {
  sanitize = {
    cfi = true
    blocklist = "./bluetooth_server_blocklist.txt"
  }
  stack_protector_ret = true
  include_dirs = [
    "include",
    "//foundation/communication/bluetooth/frameworks/inner/common",
    "//foundation/communication/bluetooth/interfaces/inner_api/include",
    "//foundation/communication/bluetooth/services/bluetooth/service/include",
    "//foundation/communication/bluetooth/services/bluetooth/service/src/ble",
    "//foundation/communication/bluetooth/services/bluetooth/service/src/permission",
    "//foundation/communication/bluetooth/services/bluetooth/stack/include",
  ]

  sources = [
    "src/bluetooth_ble_advertiser_server.cpp",
    "src/bluetooth_ble_central_manager_server.cpp",
    "src/bluetooth_gatt_client_server.cpp",
    "src/bluetooth_gatt_server_server.cpp",
    "src/bluetooth_hitrace.cpp",
    "src/bluetooth_host_dumper.cpp",
    "src/bluetooth_host_server.cpp",
    "src/bluetooth_socket_server.cpp",
    "src/bluetooth_utils_server.cpp",
  ]

  defines = []

  if (bluetooth_a2dp_sink_feature) {
    defines += [ "BLUETOOTH_A2DP_SINK_FEATURE" ]

    sources += [ "src/bluetooth_a2dp_sink_server.cpp" ]
  }

  if (bluetooth_a2dp_source_feature) {
    defines += [ "BLUETOOTH_A2DP_SRC_FEATURE" ]

    sources += [ "src/bluetooth_a2dp_source_server.cpp" ]
  }

  if (bluetooth_avrcp_ct_feature) {
    defines += [ "BLUETOOTH_AVRCP_CT_FEATURE" ]

    sources += [ "src/bluetooth_avrcp_ct_server.cpp" ]
  }

  if (bluetooth_avrcp_tg_feature) {
    defines += [ "BLUETOOTH_AVRCP_TG_FEATURE" ]

    sources += [ "src/bluetooth_avrcp_tg_server.cpp" ]
  }

  if (bluetooth_hfp_ag_feature) {
    defines += [ "BLUETOOTH_HFP_AG_FEATURE" ]

    sources += [ "src/bluetooth_hfp_ag_server.cpp" ]
  }

  if (bluetooth_hfp_hf_feature) {
    defines += [ "BLUETOOTH_HFP_HF_FEATURE" ]

    sources += [ "src/bluetooth_hfp_hf_server.cpp" ]
  }

  if (bluetooth_hid_host_feature) {
    defines += [ "BLUETOOTH_HID_HOST_FEATURE" ]

    sources += [ "src/bluetooth_hid_host_server.cpp" ]
  }

  if (bluetooth_map_client_feature) {
    defines += [ "BLUETOOTH_MAP_CLIENT_FEATURE" ]

    sources += [ "src/bluetooth_map_mce_server.cpp" ]
  }

  if (bluetooth_map_server_feature) {
    defines += [ "BLUETOOTH_MAP_SERVER_FEATURE" ]

    sources += [ "src/bluetooth_map_mse_server.cpp" ]
  }

  if (bluetooth_opp_feature) {
    defines += [ "BLUETOOTH_OPP_FEATURE" ]

    sources += [ "src/bluetooth_opp_server.cpp" ]
  }

  if (bluetooth_pan_feature) {
    defines += [ "BLUETOOTH_PAN_FEATURE" ]

    sources += [ "src/bluetooth_pan_server.cpp" ]
  }

  if (bluetooth_pbap_client_feature) {
    defines += [ "BLUETOOTH_PBAP_CLIENT_FEATURE" ]

    sources += [ "src/bluetooth_pbap_pce_server.cpp" ]
  }

  if (bluetooth_pbap_server_feature) {
    defines += [ "BLUETOOTH_PBAP_SERVER_FEATURE" ]

    sources += [ "src/bluetooth_pbap_pse_server.cpp" ]
  }

  deps = [
    "$BT_ROOT/etc/init:etc",
    "$SUBSYSTEM_DIR/bluetooth/frameworks/inner:btcommon",
    "$SUBSYSTEM_DIR/bluetooth/services/bluetooth/ipc:btipc_service",
    "$SUBSYSTEM_DIR/bluetooth/services/bluetooth/service:btservice",
    "//foundation/systemabilitymgr/safwk/interfaces/innerkits/safwk:system_ability_fwk",
    "//foundation/systemabilitymgr/samgr/interfaces/innerkits/samgr_proxy:samgr_proxy",
  ]

  external_deps = [
    "access_token:libaccesstoken_sdk",
    "c_utils:utils",
    "eventhandler:libeventhandler",
    "hisysevent_native:libhisysevent",
    "hitrace_native:hitrace_meter",
    "hiviewdfx_hilog_native:libhilog",
    "ipc:ipc_core",
  ]

  part_name = "bluetooth"
  subsystem_name = "communication"
}

group("bluetooth_codec") {
  deps = [ "$BT_ROOT/service:btsbc" ]
}
